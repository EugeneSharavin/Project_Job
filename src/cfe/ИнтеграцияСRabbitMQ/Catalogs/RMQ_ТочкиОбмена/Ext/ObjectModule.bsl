
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Действует Тогда
		 ПроверяемыеРеквизиты.Добавить("ТипТочкиОбмена");
	КонецЕсли;	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ Действует Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	1 КАК Поле1
			|ИЗ
			|	РегистрСведений.RMQ_НастройкиМаршрутизации КАК RMQ_НастройкиМаршрутизации
			|ГДЕ
			|	RMQ_НастройкиМаршрутизации.ТочкаОбмена = &ТочкаОбмена
			|	И RMQ_НастройкиМаршрутизации.Действует";
		
		Запрос.УстановитьПараметр("ТочкаОбмена", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Отказ = Истина;
			Сообщить(НСтр("ru = 'Точка обмена используется в действующих настройках маршрутизации! Отключить признак ""Действует"" нельзя!'"));
			Возврат;
		КонецЕсли;  
	КонецЕсли;
	
	НастройкиПодключения = RMQ_ОбменСообщениями.ПолучитьНастройкиПодключенияRabbitMQ(ВиртуальныйХост);
	
	КлиентКомпоненты = RMQ_ОбменСообщениями.ПолучитьКомпонентуСервер();
	
	Попытка
		КлиентКомпоненты.Connect(
			НастройкиПодключения.Адрес,
			НастройкиПодключения.Порт,
			НастройкиПодключения.ИмяПользователя,
			НастройкиПодключения.Пароль,
			НастройкиПодключения.ВиртуальныйХост);
			
		ТочкаОбмена = Наименование;
		
		Если Действует Тогда
			КлиентКомпоненты.DeclareExchange(ТочкаОбмена, НРег(Строка(ТипТочкиОбмена)), Ложь, Истина, Ложь);
			Сообщить("Точка обмена успешно создана!");
		Иначе
			КлиентКомпоненты.DeleteExchange(ТочкаОбмена, Ложь);
			Сообщить("Точка обмена успешно удалена!");
		КонецЕсли;
	Исключение
		СистемнаяОшибка = ОписаниеОшибки();
		ТекстСообщения = "Ошибка создания точки обмена!%СистемнаяОшибка%";
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если Действует Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Точка обмена действует! Перед удалением отключите признак ""Действует"" у точки обмена.'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли