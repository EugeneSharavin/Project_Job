#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	Если НЕ Действует Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	1 КАК Поле1
			|ИЗ
			|	РегистрСведений.RMQ_НастройкиМаршрутизации КАК RMQ_НастройкиМаршрутизации
			|ГДЕ
			|	RMQ_НастройкиМаршрутизации.Очередь = &Очередь
			|	И RMQ_НастройкиМаршрутизации.Действует";
		
		Запрос.УстановитьПараметр("Очередь", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Отказ = Истина;
			Сообщить(НСтр("ru = 'Очередь используется в действующих настройках маршрутизации! Отключить признак ""Действует"" нельзя!'"));
			Возврат;
		КонецЕсли;  
	КонецЕсли;

	НастройкиПодключения = RMQ_ОбменСообщениями.ПолучитьНастройкиПодключенияRabbitMQ(ВиртуальныйХост);

	КлиентКомпоненты = RMQ_ОбменСообщениями.ПолучитьКомпонентуСервер();
	
	Попытка
		КлиентКомпоненты.Connect(
			НастройкиПодключения.Адрес,
			НастройкиПодключения.Порт,
			НастройкиПодключения.ИмяПользователя,
			НастройкиПодключения.Пароль,
			НастройкиПодключения.ВиртуальныйХост);
			
		ИмяОчереди = Наименование;
		
		Если Действует Тогда
			КлиентКомпоненты.DeclareQueue(ИмяОчереди, Ложь, Истина, Ложь, Ложь);
			Сообщить("Очередь успешно создана!");
		Иначе
			КлиентКомпоненты.DeleteQueue(ИмяОчереди, Истина, Истина);
			Сообщить("Очередь успешно удалена!");
		КонецЕсли;
	Исключение
		СистемнаяОшибка = ОписаниеОшибки();
		ТекстСообщения = "Ошибка создания очереди!%СистемнаяОшибка%";
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если Действует Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Очередь действует! Перед удалением отключите признак ""Действует"" у очереди.'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли