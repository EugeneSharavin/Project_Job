
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.НепрочитанныеСообщения.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ОбновитьОчередьСообщенийНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьОчередьСообщенийНаСервере()
	КлиентКомпоненты = RMQ_ОбменСообщениями.ПолучитьКомпонентуСервер();
	НепрочитанныеСообщения.Загрузить(RMQ_ОбменСообщениями.ПрочитатьСообщения(КлиентКомпоненты, Объект.Наименование, Ложь, Ложь));
	
	Элементы.НепрочитанныеСообщения.Заголовок = СтрШаблон(НСтр("ru = 'Непрочитанные сообщения (%1)'"), НепрочитанныеСообщения.Количество());
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОчередьСообщений(Команда)
	ОбновитьОчередьСообщенийНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчиститьОчередьСообщенийНаСервере()
	КлиентКомпоненты = RMQ_ОбменСообщениями.ПолучитьКомпонентуСервер();
	RMQ_ОбменСообщениями.ПрочитатьСообщения(КлиентКомпоненты, Объект.Наименование, Ложь);
	
	ОбновитьОчередьСообщенийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОчередьСообщений(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчисткеОчередиСообщений", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Вы хотите удалить все сообщения из очереди?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОчисткеОчередиСообщений(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьОчередьСообщенийНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Элементы.НепрочитанныеСообщения.Видимость = Истина;
КонецПроцедуры
