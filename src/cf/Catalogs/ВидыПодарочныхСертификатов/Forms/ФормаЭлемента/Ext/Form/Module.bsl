
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)                                   
	Элементы.Действует.Видимость = НЕ Объект.Ссылка.Пустая();
	ДействуетПриИзмененииНаСервере();
	
	ШагНоминальнойСтоимости = Константы.ШагНоминальнойСтоимостиПодарочныхСертификатов.Получить();
	Элементы.НоминальнаяСтоимость.КнопкаРегулирования = ШагНоминальнойСтоимости > 0;
	ВариантИспользования = ?(Объект.МногократноеИспользование, "Многократное", "Однократное");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.МногократноеИспользование = ВариантИспользования = "Многократное";
КонецПроцедуры

&НаКлиенте
Процедура НоминальнаяСтоимостьРегулирование(Элемент, Направление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Объект.НоминальнаяСтоимость = Объект.НоминальнаяСтоимость + ШагНоминальнойСтоимости * Направление;
	
	Если НЕ ЗначениеЗаполнено(Объект.НоминальнаяСтоимость) Тогда
		Объект.НоминальнаяСтоимость = ШагНоминальнойСтоимости;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НоминальнаяСтоимостьПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ШагНоминальнойСтоимости) Тогда
		Если Объект.НоминальнаяСтоимость % ШагНоминальнойСтоимости <> 0 Тогда
			КоличествоШагов = Цел(Объект.НоминальнаяСтоимость / ШагНоминальнойСтоимости);
			Разница = Объект.НоминальнаяСтоимость - ШагНоминальнойСтоимости * КоличествоШагов;
			
			Если Разница >= ШагНоминальнойСтоимости / 2 Тогда
				Объект.НоминальнаяСтоимость = ШагНоминальнойСтоимости * (КоличествоШагов + 1);
			Иначе
				Объект.НоминальнаяСтоимость = ШагНоминальнойСтоимости * КоличествоШагов;
				
				Если НЕ ЗначениеЗаполнено(Объект.НоминальнаяСтоимость) Тогда
					Объект.НоминальнаяСтоимость = ШагНоминальнойСтоимости;
				КонецЕсли;
			КонецЕсли;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Скорректирована номинальная стоимость согласно установленному шагу'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьНаименование(Команда)
	Если ЗначениеЗаполнено(Объект.Наименование) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОПерезаписиНаименования", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Наименование не пустое. Вы хотите перезаполнить наименование согласно введенным данным?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		СгенерироватьНаименованиеВидаПодарочныхСертификатов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОПерезаписиНаименования(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СгенерироватьНаименованиеВидаПодарочныхСертификатов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьНаименованиеВидаПодарочныхСертификатов()
	Объект.Наименование = СтрШаблон(НСтр("ru = '%1 %2 (%3 мес, %4, %5)'"), 
								Объект.НоминальнаяСтоимость,
								Объект.Валюта,
								Объект.СрокДействия,
								?(Объект.Возвратный, НСтр("ru = 'возвратный'"), НСтр("ru = 'невозвратный'")),
							    ?(Объект.МногократноеИспользование, НСтр("ru = 'многократный'"), НСтр("ru = 'однократный'")));
КонецПроцедуры

&НаКлиенте
Процедура ДействуетПриИзменении(Элемент)
	Если НЕ Объект.Действует Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПриОтключенииПризнакаДействует", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'После отключения признака ""Действует"" будет невозможно сгенерировать и активировать ранее сгенерированные сертификаты данного вида! Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ДействуетПриИзмененииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПриОтключенииПризнакаДействует(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДействуетПриИзмененииНаСервере();
	Иначе
		Объект.Действует = Истина;
	КонецЕсли;
КонецПроцедуры
									
&НаСервере
Процедура ДействуетПриИзмененииНаСервере()
	Элементы.Действует.Подсказка = ?(Объект.Действует, 
										НСтр("ru = 'Вид подарочных сертификатов является действующим. 
												   |Разрешен ввод новых сертификатов.'"),
										НСтр("ru = 'Вид подарочных сертификатов является не действующим. 
												   |Ввод новых сертификатов запрещен. 
												   |Ранее активированные сертификаты действительны.'"));
										
	Элементы.Наименование.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.Код.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.НоминальнаяСтоимость.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.Валюта.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.СрокДействия.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.Возвратный.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.ВариантИспользования.ТолькоПросмотр = НЕ Объект.Действует;
	Элементы.ШаблонНомера.ТолькоПросмотр = НЕ Объект.Действует;
	
	Элементы.ФормаОбработкаГенерацияПодарочныхСертификатовСгенерироватьПодарочныеСертификаты.Доступность = Объект.Действует;
	Элементы.СгенерироватьНаименование.Доступность = Объект.Действует;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ВидПодарочныхСертификатов_Запись");
КонецПроцедуры
									
									
	
	