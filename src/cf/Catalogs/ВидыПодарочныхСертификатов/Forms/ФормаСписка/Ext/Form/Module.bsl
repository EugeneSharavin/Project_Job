
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ШагНоминальнойСтоимости = Константы.ШагНоминальнойСтоимостиПодарочныхСертификатов.Получить();
	КонтролироватьШагНоминальнойСтоимости = ЗначениеЗаполнено(ШагНоминальнойСтоимости);
	Элементы.ШагНоминальнойСтоимости.Видимость = КонтролироватьШагНоминальнойСтоимости;
КонецПроцедуры

&НаКлиенте
Процедура КонтролироватьШагНоминальнойСтоимостиПриИзменении(Элемент)
	Элементы.ШагНоминальнойСтоимости.Видимость = КонтролироватьШагНоминальнойСтоимости;
	
	Если НЕ КонтролироватьШагНоминальнойСтоимости Тогда
		ШагНоминальнойСтоимости = 0;
		УстановитьШагНоминальнойСтоимости(ШагНоминальнойСтоимости);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьШагНоминальнойСтоимости(ШагНоминальнойСтоимости)
	Если ШагНоминальнойСтоимости > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ПодарочныйСертификат.Владелец КАК ВидПодарочногоСертификата,
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ПодарочныйСертификат.Владелец.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ПодарочныйСертификат.Владелец.Валюта КАК Валюта
			|ИЗ
			|	РегистрСведений.ИсторияСтатусовПодарочныхСертификатов.СрезПоследних КАК ИсторияСтатусовПодарочныхСертификатовСрезПоследних
			|ГДЕ
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.Активирован), ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ЧастичноИспользован))";
		
		РезультатЗапроса = Запрос.Выполнить();              
		
		Если НЕ РезультатЗапроса.Пустой() Тогда		
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Обнаружен вид действующих подарочных сертификатов, номинальная сумма которых не соответствует указанному шагу:%1'"), Символы.ПС);
			Иначе
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Обнаружены виды действующих подарочных сертификатов, номинальная сумма которых не соответствует указанному шагу:%1'"), Символы.ПС);
			КонецЕсли;
			
			ЕстьНекратныеНоминалы = Ложь;
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ВыборкаДетальныеЗаписи.НоминальнаяСтоимость % ШагНоминальнойСтоимости <> 0 Тогда
					ТекстСообщения = ТекстСообщения + СтрШаблон("%1 %2 %3 %4", Символы.ПС, ВыборкаДетальныеЗаписи.ВидПодарочногоСертификата, ВыборкаДетальныеЗаписи.НоминальнаяСтоимость, ВыборкаДетальныеЗаписи.Валюта);
					ЕстьНекратныеНоминалы = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьНекратныеНоминалы Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
                ШагНоминальнойСтоимости = Константы.ШагНоминальнойСтоимостиПодарочныхСертификатов.Получить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Константы.ШагНоминальнойСтоимостиПодарочныхСертификатов.Установить(ШагНоминальнойСтоимости);
КонецПроцедуры

&НаКлиенте
Процедура ШагНоминальнойСтоимостиПриИзменении(Элемент)
	УстановитьШагНоминальнойСтоимости(ШагНоминальнойСтоимости);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если КонтролироватьШагНоминальнойСтоимости И НЕ ЗначениеЗаполнено(ШагНоминальнойСтоимости) Тогда
		СтандартнаяОбработка = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Шаг номинальной стоимости"" не заполнено.'"), , "ШагНоминальнойСтоимости", , Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если КонтролироватьШагНоминальнойСтоимости Тогда
		СтандартнаяОбработка = ПроверитьЗаполнение();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандыСгенерироватьПодарочныеСертификаты()
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.ФормаОбработкаГенерацияПодарочныхСертификатовСгенерироватьПодарочныеСертификаты.Доступность = ТекущиеДанные.Действует;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ОбновитьДоступностьКомандыСгенерироватьПодарочныеСертификаты();
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если КонтролироватьШагНоминальнойСтоимости Тогда
		Отказ = НЕ ПроверитьЗаполнение();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ВидПодарочныхСертификатов_Запись" Тогда
		Элементы.Список.Обновить();
		ОбновитьДоступностьКомандыСгенерироватьПодарочныеСертификаты();
	КонецЕсли;
КонецПроцедуры
