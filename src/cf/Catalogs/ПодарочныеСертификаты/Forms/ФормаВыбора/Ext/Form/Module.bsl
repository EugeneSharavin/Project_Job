
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ТолькоДействующиеВиды") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец.Действует", Истина, 
				ВидСравненияКомпоновкиДанных.Равно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
			
	Если Параметры.Свойство("ТолькоНеАктивированные") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПодарочныеСертификаты.Ссылка КАК ПодарочныйСертификат
			|ИЗ
			|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСтатусовПодарочныхСертификатов КАК ИсторияСтатусовПодарочныхСертификатов
			|		ПО (ИсторияСтатусовПодарочныхСертификатов.ПодарочныйСертификат = ПодарочныеСертификаты.Ссылка)
			|ГДЕ
			|	ИсторияСтатусовПодарочныхСертификатов.Период ЕСТЬ NULL";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		НеАктивированныеСертификаты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ПодарочныйСертификат");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", НеАктивированныеСертификаты, 
				ВидСравненияКомпоновкиДанных.ВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоАктивированные") И Параметры.Свойство("Дата") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ПодарочныйСертификат КАК ПодарочныйСертификат
			|ПОМЕСТИТЬ ВТ_ДействующиеСертификаты
			|ИЗ
			|	РегистрСведений.ИсторияСтатусовПодарочныхСертификатов.СрезПоследних(&Дата, ) КАК ИсторияСтатусовПодарочныхСертификатовСрезПоследних
			|ГДЕ
			|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.Активирован), ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ЧастичноИспользован))
			|	И ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ДействуетДо >= КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПодарочныеСертификатыОстатки.ПодарочныйСертификат КАК ПодарочныйСертификат,
			|	ПодарочныеСертификатыОстатки.СуммаОстаток КАК ОстатокСтоимости
			|ПОМЕСТИТЬ ВТ_ОстаткиСтоимости
			|ИЗ
			|	РегистрНакопления.ПодарочныеСертификаты.Остатки(&Дата, ) КАК ПодарочныеСертификатыОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ПодарочныйСертификат
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_ДействующиеСертификаты.ПодарочныйСертификат КАК ПодарочныйСертификат,
			|	ВТ_ОстаткиСтоимости.ОстатокСтоимости КАК ОстатокСтоимости
			|ИЗ
			|	ВТ_ДействующиеСертификаты КАК ВТ_ДействующиеСертификаты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОстаткиСтоимости КАК ВТ_ОстаткиСтоимости
			|		ПО ВТ_ДействующиеСертификаты.ПодарочныйСертификат = ВТ_ОстаткиСтоимости.ПодарочныйСертификат";
		
		Запрос.УстановитьПараметр("Дата", Параметры.Дата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		АктивированныеСертификаты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ПодарочныйСертификат");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", АктивированныеСертификаты, 
				ВидСравненияКомпоновкиДанных.ВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Параметры.Свойство("Валюта") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец.Валюта", Параметры.Валюта, 
				ВидСравненияКомпоновкиДанных.Равно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоВозвратные") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец.Возвратный", Истина, 
				ВидСравненияКомпоновкиДанных.Равно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
КонецПроцедуры
