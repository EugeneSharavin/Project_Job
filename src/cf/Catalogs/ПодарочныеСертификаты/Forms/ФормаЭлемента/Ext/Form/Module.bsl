
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ДекорацияНадписьНеДействует.Видимость = 
		НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Действует");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.Статус КАК Статус,
		|	ИсторияСтатусовПодарочныхСертификатовСрезПоследних.ДействуетДо КАК ДействуетДо
		|ПОМЕСТИТЬ ВТ_ТекущийСтатус
		|ИЗ
		|	РегистрСведений.ИсторияСтатусовПодарочныхСертификатов.СрезПоследних(, ПодарочныйСертификат = &Ссылка) КАК ИсторияСтатусовПодарочныхСертификатовСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодарочныеСертификатыОстатки.СуммаОстаток КАК НеиспользованныйОстатокСтоимости
		|ПОМЕСТИТЬ ВТ_НеиспользованныйОстатокСтоимости
		|ИЗ
		|	РегистрНакопления.ПодарочныеСертификаты.Остатки(, ПодарочныйСертификат = &Ссылка) КАК ПодарочныеСертификатыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВТ_ТекущийСтатус.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ПустаяСсылка)
		|			ТОГДА ""Не активирован""
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ВТ_ТекущийСтатус.Статус)
		|	КОНЕЦ КАК ТекущийСтатус,
		|	ЕСТЬNULL(ВТ_ТекущийСтатус.ДействуетДо, ДАТАВРЕМЯ(1,1,1)) КАК ДействуетДо,
		|	ЕСТЬNULL(ВТ_НеиспользованныйОстатокСтоимости.НеиспользованныйОстатокСтоимости, 0) КАК НеиспользованныйОстатокСтоимости
		|ИЗ
		|	ВТ_ТекущийСтатус КАК ВТ_ТекущийСтатус,
		|	ВТ_НеиспользованныйОстатокСтоимости КАК ВТ_НеиспользованныйОстатокСтоимости";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);
	Иначе
		ТекущийСтатус = "Не активирован";
		ДействуетДо = Дата(1,1,1);
		НеиспользованныйОстатокСтоимости = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
