
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтогоСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтогоСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтогоСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатыОкончанияДействияСертификатов()
	МассивСертификатов = Объект.ПодарочныеСертификаты.Выгрузить(, "ПодарочныйСертификат").ВыгрузитьКолонку("ПодарочныйСертификат");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодарочныеСертификаты.Ссылка КАК ПодарочныйСертификат,
		|	ДОБАВИТЬКДАТЕ(&Дата, МЕСЯЦ, ПодарочныеСертификаты.Владелец.СрокДействия) КАК ДействуетДо
		|ИЗ
		|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка В(&МассивСертификатов)";
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("МассивСертификатов", МассивСертификатов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();                 
	
	СтруктураПоиска = Новый Структура("ПодарочныйСертификат");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаДетальныеЗаписи);
		Строки = Объект.ПодарочныеСертификаты.НайтиСтроки(СтруктураПоиска);
		Строки[0].ДействуетДо = ВыборкаДетальныеЗаписи.ДействуетДо;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ПодарочныеСертификаты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ПодарочныйСертификат) Тогда
        ТекущиеДанные.ДействуетДо = ПодарочныеСертификатыСервер.ПолучитьДатуОкончанияДействияСертификата(ТекущиеДанные.ПодарочныйСертификат, Объект.Дата);
	Иначе
		ТекущиеДанные.ДействуетДо = Дата(1,1,1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.ФормаВыбора", Новый Структура("ТолькоДействующиеВиды, ТолькоНеАктивированные"), Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПриИзменении(Элемент)
	ИтогоСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Функция ПолучитьИтоговуюСтоимостьПоВалютам()
	ИтоговыеСтоимостиПоВалютам = Новый Соответствие;
	
	МассивСертификатов = Объект.ПодарочныеСертификаты.Выгрузить(, "ПодарочныйСертификат").ВыгрузитьКолонку("ПодарочныйСертификат");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ПодарочныеСертификаты.Владелец.НоминальнаяСтоимость) КАК НоминальнаяСтоимость,
		|	ПодарочныеСертификаты.Владелец.Валюта КАК Валюта
		|ИЗ
		|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка В(&МассивСертификатов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодарочныеСертификаты.Владелец.Валюта";
	
	Запрос.УстановитьПараметр("МассивСертификатов", МассивСертификатов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Результат = "";

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат = Результат + ?(ЗначениеЗаполнено(Результат), Символы.ПС, "") + СтрШаблон("%1 %2", Формат(ВыборкаДетальныеЗаписи.НоминальнаяСтоимость, "ЧДЦ=2; ЧРГ=,; ЧН=0,00"), ВыборкаДетальныеЗаписи.Валюта);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции
