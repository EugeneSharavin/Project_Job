
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтоговаяСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтоговаяСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьДатыОкончанияДействияСертификатов();
	ИтоговаяСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатыОкончанияДействияСертификатов()
	МассивСертификатов = Объект.ПодарочныеСертификаты.Выгрузить(, "ПодарочныйСертификат").ВыгрузитьКолонку("ПодарочныйСертификат");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодарочныеСертификаты.Ссылка КАК ПодарочныйСертификат,
		|	ДОБАВИТЬКДАТЕ(&Дата, МЕСЯЦ, ПодарочныеСертификаты.Владелец.СрокДействия) КАК ДействуетДо
		|ИЗ
		|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка В(&МассивСертификатов)";
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("МассивСертификатов", МассивСертификатов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();                 
	
	СтруктураПоиска = Новый Структура("ПодарочныйСертификат");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаДетальныеЗаписи);
		Строки = Объект.ПодарочныеСертификаты.НайтиСтроки(СтруктураПоиска);
		Строки[0].ДействуетДо = ВыборкаДетальныеЗаписи.ДействуетДо;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ПодарочныеСертификаты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ПодарочныйСертификат) Тогда
        ТекущиеДанные.ДействуетДо = ПодарочныеСертификатыСервер.ПолучитьДатуОкончанияДействияСертификата(ТекущиеДанные.ПодарочныйСертификат, Объект.Дата);
	Иначе
		ТекущиеДанные.ДействуетДо = Дата(1,1,1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.ФормаВыбора", Новый Структура("ТолькоДействующиеВиды, ТолькоНеАктивированные"), Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПриИзменении(Элемент)
	ИтоговаяСтоимость = ПолучитьИтоговуюСтоимостьПоВалютам();
КонецПроцедуры

&НаСервере
Функция ПолучитьИтоговуюСтоимостьПоВалютам()
	МассивСертификатов = Объект.ПодарочныеСертификаты.Выгрузить(, "ПодарочныйСертификат").ВыгрузитьКолонку("ПодарочныйСертификат");
	Возврат ПодарочныеСертификатыСервер.РеализацияПодарочныхСертификатовПолучитьИтоговуюСтоимостьПоВалютам(МассивСертификатов);
КонецФункции

&НаКлиенте
Процедура Подбор(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьПодборПодарочныхСертификатов", ПодарочныеСертификатыКлиент, Новый Структура("Форма", ЭтотОбъект)); 
	ВыбранныеПодарочныеСертификаты = ПодарочныеСертификатыКлиент.ПолучитьПодарочныеСертификатыДокумента(Объект.ПодарочныеСертификаты);
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.Форма.ФормаВыбора", Новый Структура("МножественныйВыбор, ТолькоДействующиеВиды, ТолькоНеАктивированные, ВыбранныеПодарочныеСертификаты", Истина, , , ВыбранныеПодарочныеСертификаты), , ,  , , ОписаниеОповещения);
КонецПроцедуры
