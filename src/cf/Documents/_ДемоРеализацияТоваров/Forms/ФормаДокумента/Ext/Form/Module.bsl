///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоТовары = Не ОбщегоНазначения.ПустойБуферОбмена("Товары");
	Элементы.ТоварыВставитьСтроки.Доступность = ЭтоТовары;
	Элементы.ТоварыВставитьСтрокиМеню.Доступность = ЭтоТовары;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.Комментарий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		
		Элементы.ТоварыНомерСтроки.Видимость = Ложь;
		Элементы.СчетаНаОплатуНомерСтроки.Видимость = Ложь;
		
		Элементы.ГруппаОсновное.ВыравниваниеЭлементовИЗаголовков =
			ВариантВыравниванияЭлементовИЗаголовков.ЭлементыПравоЗаголовкиЛево;
		
		Элементы.ГруппаДополнительно.ВыравниваниеЭлементовИЗаголовков =
			ВариантВыравниванияЭлементовИЗаголовков.ЭлементыПравоЗаголовкиЛево;
	КонецЕсли;
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	//{ Шаравин Евгений 20.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
	ЗаполнитьВалютуВОплатеПодарочнымиСертификатами();
	//}
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	//{ Шаравин Евгений 20.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
	ЗаполнитьВалютуВОплатеПодарочнымиСертификатами();
	//}
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		ЭтоТовары = (Параметр.ИсточникКопирования = "Товары");
		Элементы.ТоварыВставитьСтроки.Доступность = ЭтоТовары;
		Элементы.ТоварыВставитьСтрокиМеню.Доступность = ЭтоТовары;
	КонецЕсли;
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов	
	УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаДокумента(ИмяСобытия, ЭтотОбъект, Источник);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	//{ Шаравин Евгений 20.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
	ЗаполнитьВалютуВОплатеПодарочнымиСертификатами();
	//}
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
&НаКлиенте
Процедура Подключаемый_ДекорацияСостояниеОригиналаНажатие()
	УчетОригиналовПервичныхДокументовКлиент.ОткрытьМенюВыбораСостояния(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	Если Элементы.Товары.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	СкопироватьСтрокиНаСервере();
	ПоказатьОповещениеПользователя(НСтр("ru = 'Копирование в буфер обмена'"), Окно.ПолучитьНавигационнуюСсылку(), 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Скопировано товаров: %1'"), Элементы.Товары.ВыделенныеСтроки.Количество()));
	Оповестить("ДанныеСкопированыВБуферОбмена", Новый Структура("ИсточникКопирования", "Товары"), Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	Количество = ВставитьСтрокиНаСервере();
	Если Количество > 0 Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Вставка из буфера обмена'"), Окно.ПолучитьНавигационнуюСсылку(), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Вставлено товаров: %1'"), Количество));
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СкопироватьСтрокиНаСервере()
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект.Товары, Элементы.Товары.ВыделенныеСтроки, "Товары");

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере()
	
	ДанныеИзБуфераОбмена = ОбщегоНазначения.СтрокиИзБуфераОбмена();
	Если ДанныеИзБуфераОбмена.Источник <> "Товары" Тогда
		Возврат 0;
	КонецЕсли;
		
	Таблица = ДанныеИзБуфераОбмена.Данные;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		ЗаполнитьЗначенияСвойств(Объект.Товары.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	Возврат Таблица.Количество();
	
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//Добавил Шаравин Евгений 20.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаСервере
Процедура ЗаполнитьВалютуВОплатеПодарочнымиСертификатами()
	Для Каждого Строка Из Объект.ОплатаПодарочнымиСертификатами Цикл 
		Строка.Валюта = Объект.Валюта;
	КонецЦикла;
	
	Элементы.ОплатаПодарочнымиСертификатамиСуммаОплаты.ТекстПодвала = Формат(Объект.ОплатаПодарочнымиСертификатами.Итог("СуммаОплаты"), "ЧДЦ=2; ЧРГ=,") + " " + Объект.Валюта;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Количество * ТекущиеДанные.Цена;
КонецПроцедуры

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Количество * ТекущиеДанные.Цена;
КонецПроцедуры

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ОплатаПодарочнымиСертификатамиПодарочныйСертификатПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОплатаПодарочнымиСертификатами.ТекущиеДанные;
	ТекущиеДанные.СуммаОплаты = 0;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ПодарочныйСертификат) Тогда
		ОстатокСтоимости = ПодарочныеСертификатыСервер.ПолучитьОстатокСтоимостиПоСертификату(ТекущиеДанные.ПодарочныйСертификат, ?(Объект.Ссылка.Пустая(), КонецДня(Объект.Дата), Объект.Дата));
		ТекущиеДанные.СуммаОплаты = Макс(Мин(Объект.Товары.Итог("Сумма") - Объект.ОплатаПодарочнымиСертификатами.Итог("СуммаОплаты"), ОстатокСтоимости), 0);
	КонецЕсли;
КонецПроцедуры

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ОплатаПодарочнымиСертификатамиПодарочныйСертификатНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.ФормаВыбора", Новый Структура("ТолькоАктивированные, Дата, Валюта", Истина, ?(Объект.Ссылка.Пустая(), КонецДня(Объект.Дата), Объект.Дата), Объект.Валюта), Элемент);
КонецПроцедуры

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ВалютаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	Если ВыбранноеЗначение <> Объект.Валюта И Объект.ОплатаПодарочнымиСертификатами.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти", ЭтотОбъект, 
										Новый Структура("СтараяВалюта", Объект.Валюта));
		ТекстВопроса = НСтр("ru = 'Внесены подарочные сертификаты в табличную часть ""Оплата подарочными сертификатами"". При изменении валюты они будут удалены. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	КонецЕсли;
КонецПроцедуры

//Добавил Шаравин Евгений 19.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ОплатаПодарочнымиСертификатами.Очистить();
		Элементы.ОплатаПодарочнымиСертификатамиСуммаОплаты.ТекстПодвала = "";
	Иначе     
		Объект.Валюта = ДополнительныеПараметры.СтараяВалюта;
	КонецЕсли;
КонецПроцедуры

//Добавил Шаравин Евгений 20.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура ОплатаПодарочнымиСертификатамиПриИзменении(Элемент)
	Элементы.ОплатаПодарочнымиСертификатамиСуммаОплаты.ТекстПодвала = Формат(Объект.ОплатаПодарочнымиСертификатами.Итог("СуммаОплаты"), "ЧДЦ=2; ЧРГ=,") + " " + Объект.Валюта;
КонецПроцедуры

//Добавил Шаравин Евгений 22.05.2025 по проектной работе курса "Архитектор 1С" (УЦ ОТУС)
&НаКлиенте
Процедура Подбор(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьПодборПодарочныхСертификатов", ПодарочныеСертификатыКлиент, Новый Структура("Форма", ЭтотОбъект)); 
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.Форма.ФормаВыбора", Новый Структура("МножественныйВыбор, ТолькоАктивированные, Дата, Валюта", Истина, Истина, ?(Объект.Ссылка.Пустая(), КонецДня(Объект.Дата), Объект.Дата), Объект.Валюта), , ,  , , ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти