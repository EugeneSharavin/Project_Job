
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВариантАннулированияПодарочныхСертификатов = ?(ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов), "выбранного вида", "всех видов");
	ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере();
	
	ПодарочныеСертификатыСервер.ЗаполнитьСведенияОПодарочныхСертификатахВДокументе(Объект);
	ПодарочныеСертификатыСервер.ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ВариантАннулированияПодарочныхСертификатов = ?(ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов), "выбранного вида", "всех видов");
	ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере();
	
	ПодарочныеСертификатыСервер.ЗаполнитьСведенияОПодарочныхСертификатахВДокументе(Объект);
	ПодарочныеСертификатыСервер.ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ВариантАннулированияПодарочныхСертификатов = ?(ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов), "выбранного вида", "всех видов");
	ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере();
	
	ПодарочныеСертификатыСервер.ЗаполнитьСведенияОПодарочныхСертификатахВДокументе(Объект);
	ПодарочныеСертификатыСервер.ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам()
	ПодарочныеСертификатыСервер.ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ПодарочныеСертификаты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ПодарочныйСертификат) Тогда
		Если ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов) И ПодарочныеСертификатыСервер.ПолучитьВидПодарочногоСертификата(ТекущиеДанные.ПодарочныйСертификат) <> Объект.ВидПодарочныхСертификатов Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выбранный подарочный сертификат не соответствует виду, указанному в шапке документа!'"));
			
			ТекущиеДанные.ПодарочныйСертификат = Неопределено;
			ТекущиеДанные.Статус = Неопределено;
			ТекущиеДанные.ОстатокСуммы = 0;
			ТекущиеДанные.ДействуетДо = Дата(1,1,1);
			
			Возврат;
		КонецЕсли;
		
		МоментПолученияСведений = ПодарочныеСертификатыСервер.ПолучитьМоментПолученияСведений(Объект.Ссылка, Объект.Дата);
		СведенияОПодарочныхСертификатах = ПодарочныеСертификатыСервер.ПолучитьСведенияОПодарочныхСертификатах(ТекущиеДанные.ПодарочныйСертификат, МоментПолученияСведений);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, СведенияОПодарочныхСертификатах[ТекущиеДанные.ПодарочныйСертификат]);
	Иначе
		ТекущиеДанные.Статус = Неопределено;
		ТекущиеДанные.ОстатокСуммы = 0;
		ТекущиеДанные.ДействуетДо = Дата(1,1,1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПодарочныйСертификатНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ПодарочныеСертификаты.ФормаВыбора", Новый Структура("ТолькоНеАннулированные, Ссылка, Дата, ВидПодарочныхСертификатов", Истина, Объект.Ссылка, Объект.Дата, Объект.ВидПодарочныхСертификатов), Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПриИзменении(Элемент)
	ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	ОбъектДокумента = РеквизитФормыВЗначение("Объект");
	ТаблицаПодарочныхСертификатов = ОбъектДокумента.ЗаполнитьПодарочныеСертификаты();
	Объект.ПодарочныеСертификаты.Загрузить(ТаблицаПодарочныхСертификатов);
	
	ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Если ВариантАннулированияПодарочныхСертификатов = "выбранного вида" И Объект.ВидПодарочныхСертификатов.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран вид подарочных сертификатов!'"), , "ВидПодарочныхСертификатов", "Объект");
		Возврат;
	КонецЕсли;
	
	Если ВариантАннулированияПодарочныхСертификатов = "всех видов" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОПерезаполненииПодарочнымиСертификатамиВсехВидов", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Внимание! Заполнение списка подарочных сертификатов всех видов может занять продолжительное время. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);                                          
	Иначе
		ПроверитьЗаполнениеТабличнойЧасти();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеТабличнойЧасти()
	Если Объект.ПодарочныеСертификаты.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОПерезаполнении", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличная часть содержит строки. При заполнении они будут удалены. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОПерезаполненииПодарочнымиСертификатамиВсехВидов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПроверитьЗаполнениеТабличнойЧасти();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОПерезаполнении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере()
	Элементы.ВидПодарочныхСертификатов.Видимость = ВариантАннулированияПодарочныхСертификатов = "выбранного вида";
	Если НЕ Элементы.ВидПодарочныхСертификатов.Видимость Тогда
		Объект.ВидПодарочныхСертификатов = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВариантАннулированияПодарочныхСертификатовПриИзменении(Элемент)
	Если Объект.ПодарочныеСертификаты.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти", ЭтотОбъект, 
										Новый Структура("СтарыйВариантАннулированияПодарочныхСертификатов, СтарыйВидПодарочныхСертификатов, СтараяВалюта, СтараяНоминальнаяСтоимость", 
															?(ВариантАннулированияПодарочныхСертификатов = "всех видов", "выбранного вида", "всех видов"), Объект.ВидПодарочныхСертификатов, Валюта, НоминальнаяСтоимость));
		ТекстВопроса = НСтр("ru = 'Документ содержит строки. При изменении варианта заполнения таблчной части она будет очищена. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере();
		ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();			
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидПодарочныхСертификатовПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов) Тогда
		Если Объект.ПодарочныеСертификаты.Количество() > 0 Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти", ЭтотОбъект, 
									Новый Структура("СтарыйВариантАннулированияПодарочныхСертификатов, СтарыйВидПодарочныхСертификатов, СтараяВалюта, СтараяНоминальнаяСтоимость", 
														ВариантАннулированияПодарочныхСертификатов, ВидПодарочныхСертификатов, Валюта, НоминальнаяСтоимость));
															
			ТекстВопроса = НСтр("ru = 'Табличная часть содержит строки. При очистке вида подарочных сертификатов они будут удалены. Продолжить?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Иначе
			ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();			
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодарочныхСертификатовОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	Если ВыбранноеЗначение <> Объект.ВидПодарочныхСертификатов Тогда
		Если Объект.ПодарочныеСертификаты.Количество() > 0 Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти", ЭтотОбъект, 
											Новый Структура("СтарыйВариантАннулированияПодарочныхСертификатов, СтарыйВидПодарочныхСертификатов, СтараяВалюта, СтараяНоминальнаяСтоимость", 
																ВариантАннулированияПодарочныхСертификатов, Объект.ВидПодарочныхСертификатов, Валюта, НоминальнаяСтоимость));
			ТекстВопроса = НСтр("ru = 'Табличная часть содержит строки. При изменении вида подарочных сертификатов они будут удалены. Продолжить?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Иначе     
			Объект.ВидПодарочныхСертификатов = ВыбранноеЗначение;
			ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОчисткеТабличнойЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ПодарочныеСертификаты.Очистить();
		ВариантАннулированияПодарочныхСертификатовПриИзмененииНаСервере();
		ЗаполнитьИтоговыеСуммыНоминаловИНеиспользованныхОстатковПоВалютам();
	Иначе
		ВариантАннулированияПодарочныхСертификатов = ДополнительныеПараметры.СтарыйВариантАннулированияПодарочныхСертификатов;
		Объект.ВидПодарочныхСертификатов = ДополнительныеПараметры.СтарыйВидПодарочныхСертификатов;
		НоминальнаяСтоимость = ДополнительныеПараметры.СтараяНоминальнаяСтоимость;
		Валюта = ДополнительныеПараметры.СтараяВалюта;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодарочныеСертификатыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если ВариантАннулированияПодарочныхСертификатов = "выбранного вида" И НЕ ЗначениеЗаполнено(Объект.ВидПодарочныхСертификатов) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран вид подарочных сертификатов!'"), , "ВидПодарочныхСертификатов", "Объект", Отказ);
	КонецЕсли;
КонецПроцедуры
