
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Сумма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПодарочныхСертификатов, "НоминальнаяСтоимость") * ПодарочныеСертификаты.Количество();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ИсторияСтатусовПодарочныхСертификатов.Записывать = Истина;
	
	СтатусВыпущен = Перечисления.СтатусыПодарочныхСертификатов.Выпущен;
	
	Для Каждого Строка Из ПодарочныеСертификаты Цикл
		ПодарочныйСертификатОбъект = Строка.ПодарочныйСертификат.ПолучитьОбъект();
		ПодарочныйСертификатОбъект.УстановитьПометкуУдаления(Ложь);
		
		Движение = Движения.ИсторияСтатусовПодарочныхСертификатов.Добавить();
		Движение.Период = Дата;
		Движение.ПодарочныйСертификат = Строка.ПодарочныйСертификат;
		Движение.Статус = СтатусВыпущен;
		Движение.Ответственный = Ответственный;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПодарочныеСертификатыСервер.ВыполнитьКонтрольПриОтменеПроведенияДокумента(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	КонтролироватьУникальностьНомеровПодарочныхСертификатовПоВсемВидам = Константы.КонтролироватьУникальностьНомеровПодарочныхСертификатовПоВсемВидам.Получить();
	
	Для Каждого Строка Из ПодарочныеСертификаты Цикл
		ПодарочныйСертификатОбъект = Строка.ПодарочныйСертификат.ПолучитьОбъект();
		ПодарочныйСертификатОбъект.ДополнительныеСвойства.Вставить("КонтролироватьУникальностьПоВсемВидам", КонтролироватьУникальностьНомеровПодарочныхСертификатовПоВсемВидам);
		ПодарочныйСертификатОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли