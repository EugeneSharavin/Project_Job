
Процедура ОбработатьПодборПодарочныхСертификатов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда   
		ЭтоРеализацияТоваров = ДополнительныеПараметры.Форма.ИмяФормы = "Документ._ДемоРеализацияТоваров.Форма.ФормаДокумента";
		
		ОбъектФормы = ДополнительныеПараметры.Форма.Объект;
		ТабличнаяЧасть = ?(ЭтоРеализацияТоваров, ОбъектФормы.ОплатаПодарочнымиСертификатами, ОбъектФормы.ПодарочныеСертификаты);
		ИмяТабличнойЧасти = ?(ЭтоРеализацияТоваров, "ОплатаПодарочнымиСертификатами", "ПодарочныеСертификаты");
		ДатаСреза = ?(ОбъектФормы.Ссылка.Пустая(), КонецДня(ОбъектФормы.Дата), ОбъектФормы.Дата);
		           
		Для Каждого ПодарочныйСертификат Из РезультатЗакрытия Цикл
			СтрокиТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ПодарочныйСертификат", ПодарочныйСертификат));
			Если СтрокиТабличнойЧасти.Количество() = 0 Тогда
				НоваяСтрока = ТабличнаяЧасть.Добавить();
				НоваяСтрока.ПодарочныйСертификат = ПодарочныйСертификат;
				
				Если ДополнительныеПараметры.Форма.ИмяФормы = "Документ.РеализацияПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					НоваяСтрока.ДействуетДо = ПодарочныеСертификатыСервер.ПолучитьДатуОкончанияДействияСертификата(ПодарочныйСертификат, ОбъектФормы.Дата);
					
				ИначеЕсли ДополнительныеПараметры.Форма.ИмяФормы = "Документ.ВводОстатковПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					НоваяСтрока.Валюта = ПодарочныеСертификатыСервер.ПолучитьРеквизитыВидаПодарочныхСертификатов(ОбъектФормы.ВидПодарочныхСертификатов, "Валюта").Валюта;
					
				ИначеЕсли ЭтоРеализацияТоваров Тогда
					
					НоваяСтрока.СуммаОплаты = 0;
					
					Если ЗначениеЗаполнено(НоваяСтрока.ПодарочныйСертификат) Тогда
						ОстатокСтоимости = ПодарочныеСертификатыСервер.ПолучитьОстатокСтоимостиПоСертификату(НоваяСтрока.ПодарочныйСертификат, ДатаСреза);
						НоваяСтрока.СуммаОплаты = Макс(Мин(ОбъектФормы.Товары.Итог("Сумма") - ОбъектФормы.ОплатаПодарочнымиСертификатами.Итог("СуммаОплаты"), ОстатокСтоимости), 0);
						НоваяСтрока.Валюта = ОбъектФормы.Валюта;
					КонецЕсли;
					
				ИначеЕсли ДополнительныеПараметры.Форма.ИмяФормы = "Документ.ВозвратПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					Если ЗначениеЗаполнено(НоваяСтрока.ПодарочныйСертификат) Тогда
				        НоваяСтрока.НеиспользованныйОстатокСтоимости = ПодарочныеСертификатыСервер.ПолучитьОстатокСтоимостиПоСертификату(НоваяСтрока.ПодарочныйСертификат, ДатаСреза);
					Иначе
						НоваяСтрока.НеиспользованныйОстатокСтоимости = 0;
					КонецЕсли;
					
				КонецЕсли;
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Подарочный сертификат %1 уже добавлен в строке %2!'"), ПодарочныйСертификат, СтрокиТабличнойЧасти[0].НомерСтроки), , "Объект." + ИмяТабличнойЧасти + "[" + Формат(СтрокиТабличнойЧасти[0].НомерСтроки - 1, "ЧГ=") + "].ПодарочныйСертификат");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры