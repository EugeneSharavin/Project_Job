
Процедура ОбработатьПодборПодарочныхСертификатов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда   
		ЭтоРеализацияТоваров = ДополнительныеПараметры.Форма.ИмяФормы = "Документ._ДемоРеализацияТоваров.Форма.ФормаДокумента";
		
		Форма = ДополнительныеПараметры.Форма;
		ОбъектФормы = Форма.Объект;
		ТабличнаяЧасть = ?(ЭтоРеализацияТоваров, ОбъектФормы.ОплатаПодарочнымиСертификатами, ОбъектФормы.ПодарочныеСертификаты);
		ИмяТабличнойЧасти = ?(ЭтоРеализацияТоваров, "ОплатаПодарочнымиСертификатами", "ПодарочныеСертификаты");
		МоментПолученияСведений = ПодарочныеСертификатыСервер.ПолучитьМоментПолученияСведений(ОбъектФормы.Ссылка, ОбъектФормы.Дата);
		           
		Для Каждого ПодарочныйСертификат Из РезультатЗакрытия Цикл
			СтрокиТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ПодарочныйСертификат", ПодарочныйСертификат));
			Если СтрокиТабличнойЧасти.Количество() = 0 Тогда
				НоваяСтрока = ТабличнаяЧасть.Добавить();
				НоваяСтрока.ПодарочныйСертификат = ПодарочныйСертификат;
				
				Если Форма.ИмяФормы = "Документ.РеализацияПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					НоваяСтрока.ДействуетДо = ПодарочныеСертификатыСервер.ПолучитьДатуОкончанияДействияСертификата(ПодарочныйСертификат, ОбъектФормы.Дата);
					
				ИначеЕсли Форма.ИмяФормы = "Документ.ВводОстатковПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					РеквизитыВидаПодарочныхСертификатов = ПодарочныеСертификатыСервер.ПолучитьРеквизитыВидаПодарочныхСертификатов(ОбъектФормы.ВидПодарочныхСертификатов, "НоминальнаяСтоимость, Валюта");
					ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыВидаПодарочныхСертификатов);
					
				ИначеЕсли ЭтоРеализацияТоваров Тогда
					
					НоваяСтрока.СуммаОплаты = 0;
					
					Если ЗначениеЗаполнено(НоваяСтрока.ПодарочныйСертификат) Тогда
						СведенияОПодарочныхСертификатах = ПодарочныеСертификатыСервер.ПолучитьСведенияОПодарочныхСертификатах(НоваяСтрока.ПодарочныйСертификат, МоментПолученияСведений);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияОПодарочныхСертификатах[НоваяСтрока.ПодарочныйСертификат]);
						НоваяСтрока.СуммаОплаты = Макс(Мин(ОбъектФормы.Товары.Итог("Сумма") - ОбъектФормы.ОплатаПодарочнымиСертификатами.Итог("СуммаОплаты"), НоваяСтрока.ОстатокСуммы), 0);
						НоваяСтрока.Валюта = ОбъектФормы.Валюта;
					КонецЕсли;
					
				ИначеЕсли Форма.ИмяФормы = "Документ.ВозвратПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					Если ЗначениеЗаполнено(НоваяСтрока.ПодарочныйСертификат) Тогда
						СведенияОПодарочныхСертификатах = ПодарочныеСертификатыСервер.ПолучитьСведенияОПодарочныхСертификатах(НоваяСтрока.ПодарочныйСертификат, МоментПолученияСведений);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияОПодарочныхСертификатах[НоваяСтрока.ПодарочныйСертификат]);
					КонецЕсли;
					
				ИначеЕсли Форма.ИмяФормы = "Документ.АннулированиеПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
					
					Если ЗначениеЗаполнено(НоваяСтрока.ПодарочныйСертификат) Тогда
						СведенияОПодарочныхСертификатах = ПодарочныеСертификатыСервер.ПолучитьСведенияОПодарочныхСертификатах(НоваяСтрока.ПодарочныйСертификат, МоментПолученияСведений);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияОПодарочныхСертификатах[НоваяСтрока.ПодарочныйСертификат]);
					КонецЕсли;
					
				КонецЕсли;
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Подарочный сертификат %1 уже добавлен в строке %2!'"), ПодарочныйСертификат, СтрокиТабличнойЧасти[0].НомерСтроки), , "Объект." + ИмяТабличнойЧасти + "[" + Формат(СтрокиТабличнойЧасти[0].НомерСтроки - 1, "ЧГ=") + "].ПодарочныйСертификат");
			КонецЕсли;
		КонецЦикла;
		
		Если Форма.ИмяФормы = "Документ.ВводОстатковПодарочныхСертификатов.Форма.ФормаДокумента" ИЛИ
			 Форма.ИмяФормы = "Документ.ВозвратПодарочныхСертификатов.Форма.ФормаДокумента" ИЛИ
			 Форма.ИмяФормы = "Документ.АннулированиеПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
			 
			ПодарочныеСертификаты = ПолучитьПодарочныеСертификатыДокумента(ОбъектФормы.ПодарочныеСертификаты);
			Форма.ИтоговыеСуммы = ПодарочныеСертификатыСервер.ПолучитьПредставлениеИтоговыхСуммНоминаловИНеиспользованныхОстатковПоВалютам(ПодарочныеСертификаты, Форма.Объект.Ссылка, Форма.Объект.Дата);
			
		ИначеЕсли Форма.ИмяФормы = "Документ.РеализацияПодарочныхСертификатов.Форма.ФормаДокумента" Тогда
			
			ПодарочныеСертификаты = ПолучитьПодарочныеСертификатыДокумента(ОбъектФормы.ПодарочныеСертификаты);
			Форма.ИтоговаяСтоимость = ПодарочныеСертификатыСервер.РеализацияПодарочныхСертификатовПолучитьИтоговуюСтоимостьПоВалютам(ПодарочныеСертификаты);
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПодарочныеСертификатыДокумента(ТабличнаяЧасть) Экспорт
	ПодарочныеСертификаты = Новый Массив;
	
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		ПодарочныеСертификаты.Добавить(Строка.ПодарочныйСертификат);
	КонецЦикла;
	
	Возврат ПодарочныеСертификаты;
КонецФункции